;通常攻撃
;スキルのテンプレートになっています

;스킬명
@SKILL_NAME_3545,ARG
RESULTS = 리본ㆍ부스트
IF ARG > -1
	SIF SKILLGAGE_H_GET(ARG,3545) == 1
		RESULTS = B･서니 빔
	SIF SKILLGAGE_H_GET(ARG,3545) == 2
		;捏造なんだよなぁ
		RESULTS = B･블루 스타
	SIF SKILLGAGE_H_GET(ARG,3545) == 3
		RESULTS = B･힐링 S
	SIF SKILLGAGE_H_GET(ARG,3545) == 4
		RESULTS = B･캐롯 S
ENDIF
;　명중
@SKILL_HITRATE_3545, ARG
RETURN 100

;　위력
@SKILL_POWER_3545, ARG
RETURN 50

;　최저공격횟수
@SKILL_MINATTACKNUMBER_3545,ARG
IF ARG > -1
	;サニービーム　火炎
	SIF SKILLGAGE_H_GET(ARG,3545) == 1
		RETURN 3
	;ブルースター　飛具
	SIF SKILLGAGE_H_GET(ARG,3545) == 2
		RETURN 4
	;ヒーリングスタッフ
	SIF SKILLGAGE_H_GET(ARG,3545) == 3
		RETURN 3
	;キャロットシューター
	SIF SKILLGAGE_H_GET(ARG,3545) == 4
		RETURN 3
ENDIF
RETURN 1

;　최대공격횟수
@SKILL_MAXATTACKNUMBER_3545,ARG
IF ARG > -1
	;サニービーム　火炎
	SIF SKILLGAGE_H_GET(ARG,3545) == 1
		RETURN 3
	;ブルースター　飛具
	SIF SKILLGAGE_H_GET(ARG,3545) == 2
		RETURN 4
	;ヒーリングスタッフ
	SIF SKILLGAGE_H_GET(ARG,3545) == 3
		RETURN 3
	;キャロットシューター
	SIF SKILLGAGE_H_GET(ARG,3545) == 4
		RETURN 3
ENDIF
RETURN 1

;　2 ＨＰ지불　3 ＭＰ지불
@SKILL_COSTTYPE_3545, ARG
RETURN 3
;소비없음＝1

;　코스트량
@SKILL_COST_3545,ARG
RETURN 45

;　상성
@SKILL_TYPE_3545,ARG
#LOCALSSIZE 3
#LOCALSIZE 4
SIF ARG < 0 || 스킬속성표시중
	RETURN GET_TYPE_NUM("만능")
SIF CFLAG:ARG:타겟 > 16 || CFLAG:ARG:타겟 < 1
	RETURN GET_TYPE_NUM("만능")
LOCAL = POS(CFLAG:ARG:타겟)
SIF LOCAL < 0
	RETURN GET_TYPE_NUM("만능")
	
LOCAL:2 = -300
LOCALS = 만능
LOCALS:1 = 만능
LOCALS:2 = 만능
	;サニービーム
	SIF SKILLGAGE_H_GET(ARG,3545) == 1
		LOCALS:2 = 화염
	;ブルースター
	SIF SKILLGAGE_H_GET(ARG,3545) == 2
		LOCALS:2 = 비구
	;ヒーリングスタッフ
	SIF SKILLGAGE_H_GET(ARG,3545) == 3
		LOCALS:2 = 충격
	;キャロットシューター
	SIF SKILLGAGE_H_GET(ARG,3545) == 4
		LOCALS:2 = 핵열

FOR LOCAL:1, 1, 3
	;判定用に数値を仕舞う。反射は-200扱い
	LOCAL:3 = MAXBASE:LOCAL:(LOCALS:(LOCAL:1)) == 999 ? -200 # MAXBASE:LOCAL:(LOCALS:(LOCAL:1))
	;LOCAL:3 = BTL_CHK_SKILL_관통(ARG) && INRANGE(MAXBASE:LOCAL:(LOCALS:(LOCAL:1)), 0, 100) ? 100 # LOCAL:3
	LOCAL:3 = CFLAG:LOCAL:(LOCALS:(LOCAL:1)+"가드킬") && LOCAL:3 < 100 ? 100 # LOCAL:3
	LOCAL:3 = CFLAG:LOCAL:(LOCALS:(LOCAL:1)+"무효화횟수") ? 0 # LOCAL:3
	LOCAL:3 = GET_TYPE_NUM(LOCALS:(LOCAL:1)) < 17 && CFLAG:LOCAL:무효플래그 ? 0 # LOCAL:3
	SIF LOCAL:1 == 1
		LOCAL:3 = LOCAL:1 == 1 && CFLAG:LOCAL:물리반사플래그 ? -200 # LOCAL:3
	IF LOCAL:3 > LOCAL:2
		LOCAL:2 = LOCAL:3
		LOCALS = %LOCALS:(LOCAL:1)%
	ENDIF
	NEXT
	PRINT 속성상성：
	PRINTS LOCALS
	RETURN GET_TYPE_NUM(LOCALS)

;　1 EXTRA　2 MAGIC
@SKILL_DECIDE_TYPE_3545, ARG
RETURN 0
;どちらでもない

;　1 물리데미지로 계산　2 마법데미지로 계산
@SKILL_DAMAGETYPE_3545, ARG
RETURN 1


;　대상　1 적　2 아군　3 양쪽전체　4 특수
@SKILL_TARGET_3545
RETURN 4

;　사거리
@SKILL_RANGE_3545,ARG
RETURN 3

;　범위　1 1체　2 1열　3 전체　4 적아군전체
@SKILL_SPHERE_3545,ARG
RETURN 1

;　 효과타입　1 공격、2 회복、3 상태변화、4 특수、5 상태회복
@SKILL_EFECT_3545
RETURN 6

;　 특수처리 여부 및 처리내용
@SKILL_SPECIALEFECT_3545,ARG
;ARG = 1인 경우는 처리 여부를 반환합니다
SIF ARG == 1
	RETURN 0
;아래쪽에 특수처리를 기술합니다

;계승타입
@SKILL_SUCCESSION_TYPE_3545
RETURN 22
;검격

;속도보정
@SKILL_SPEED_3545, ARG = -1
RETURN 0

;　구상의 표시와 그 타입
@SKILL_MESSAGE_3545,ARG


;전투시 이외에 사용 가능한가
@SKILL_ACTIONABLE_FIELD_3545
RETURN 0

;스킬변화？용 랭크
@SKILL_RANK_3545
RETURN 0


;템플릿용、추가효과 시리즈
;추가효과 스테이터스
@SKILL_ADDTIONAL_STATE_3545,ARG
RETURN 0

;추가효과 상성
@SKILL_ADDTIONAL_TYPE_3545,ARG
RETURN 0

;추가효과 기본부여확률
@SKILL_ADDTIONAL_BASE_RATE_3545,ARG
RETURN 0

;추가효과 부여확률상한
@SKILL_ADDTIONAL_MAX_RATE_3545,ARG
RETURN 0
;====================================================
;カウンターは一旦オミット。
;====================================================
;@SKILL_COUNTER_ACTIONABLE_3545,ARG,ARG:1
;SIF SKILLGAGE_D_GET(ARG,3545) < 1
;	RETURN 0
;SIF !GETBIT(CFLAG:ARG:받음플래그,18)
;	RETURN 0
;SIF ARG:1 < 0 || GET_STATE(CFLAG:(ARG:1):상태이상) == "DYING"
;	RETURN 0
;CALL SKILLGAGE_H_SET,ARG,3545,1
;RETURN 1
;
;====================================================
;トリガー発動条件
;====================================================
@SKILL_TRIGGER_ACTIONABLE_3545,ARG,ARG:1
;自分じゃなければNG
SIF ARG != ARG:1
	RETURN 0
SIF SKILLGAGE_D_GET(ARG,3545) < 1
	RETURN 0
CALLFORM SKILL_EFECT_{CFLAG:ARG:입력행동}
SIF RESULT != 1 || CFLAG:ARG:타겟 < 1
	RETURN 0
IF CFLAG:ARG:타겟 < 17
	SIF POS(CFLAG:ARG:타겟) < 0 || GET_STATE(CFLAG:POS(CFLAG:ARG:타겟):상태이상) == "DYING"
		RETURN 0
	RESULT:1 = CFLAG:ARG:타겟
ELSE
	RESULT:1 = CFLAG:ARG:타겟
ENDIF
RETURN 1

;====================================================
;스킬 및 행동의 실행
;====================================================
@ACTION_3545,ARG,ARG:1
#LOCALSIZE 4
IF SKILLGAGE_D_GET(ARG,3545) == 0
		IF SKILLGAGE_F_GET(ARG,3545) == 100
			CALL SKILLGAGE_D_SET,ARG,3545,6
			CALL SKILLGAGE_F_SET,ARG,3545,0
			PRINTFORML 맥스 부스트！！　6턴 간、추격이 발동되게 되었다
		ELSE
			CALL SKILLGAGE_D_SET,ARG,3545,3
			CALL SKILLGAGE_F_SET,ARG,3545,0
			PRINTFORML 리본 부스트 발동！　3턴 간、추격이 발동되게 되었다
		ENDIF
	IF CFLAG:(ARG):PT플래그 == 0
		LOCAL:1 = 7
		LOCAL:2 = 17
	ELSE
		LOCAL:1 = 1
		LOCAL:2 = 7
	ENDIF
FOR LOCAL,LOCAL:1,LOCAL:2
	SIF POS(LOCAL) < 0
		CONTINUE
	CFLAG:POS(LOCAL):명중강화 = MIN(32, CFLAG:POS(LOCAL):명중강화 + 4)
	CFLAG:POS(LOCAL):회피강화 = MIN(32, CFLAG:POS(LOCAL):회피강화 + 4)
NEXT
		PRINTFORML \@ CFLAG:ARG:PT플래그 == 0 ? 적 # 아군 \@의 속도（명중ㆍ회피）업！
	IF SKILLGAGE_H_GET(ARG,3545) == 1
		;1サニービーム　　　　　　　　（火炎追撃　速度ㆍ攻撃強化）
		FOR LOCAL,LOCAL:1,LOCAL:2
			SIF POS(LOCAL) < 0
			CONTINUE
		CFLAG:POS(LOCAL):공격강화 = MIN(32, CFLAG:POS(LOCAL):공격강화 + 4)
		NEXT
			PRINTFORMW \@ CFLAG:ARG:PT플래그 == 0 ? 적 # 아군 \@의 공격력 업！
	ELSEIF SKILLGAGE_H_GET(ARG,3545) == 2
		;2ブルースター　　　　　　　　（飛具追撃　速度ㆍ防御強化）
		FOR LOCAL,LOCAL:1,LOCAL:2
			SIF POS(LOCAL) < 0
			CONTINUE
		CFLAG:POS(LOCAL):방어강화 = MIN(32, CFLAG:POS(LOCAL):방어강화 + 4)
		NEXT
			PRINTFORMW \@ CFLAG:ARG:PT플래그 == 0 ? 적 # 아군 \@의 방어력 업！
	ELSEIF SKILLGAGE_H_GET(ARG,3545) == 3
		;3ヒーリングスタッフ　　　　　（衝撃追撃　速度＋２）
		;メディアこれ機能しねーな？ということで速度＋２に変更。回復無尽蔵もよろしくないし
		FOR LOCAL,LOCAL:1,LOCAL:2
			SIF POS(LOCAL) < 0
			CONTINUE
	CFLAG:POS(LOCAL):명중강화 = MIN(32, CFLAG:POS(LOCAL):명중강화 + 4)
	CFLAG:POS(LOCAL):회피강화 = MIN(32, CFLAG:POS(LOCAL):회피강화 + 4)
		NEXT
	ELSEIF SKILLGAGE_H_GET(ARG,3545) == 4
		;4キャロットシューター　　　　（核熱追撃　速度ㆍ魔法威力強化）
		FOR LOCAL,LOCAL:1,LOCAL:2
			SIF POS(LOCAL) < 0
			CONTINUE
		CFLAG:POS(LOCAL):마법위력강화 = MIN(32, CFLAG:POS(LOCAL):마법위력강화 + 4)
		NEXT
			PRINTFORMW \@ CFLAG:ARG:PT플래그 == 0 ? 적 # 아군 \@의 마법위력 업！		
	ENDIF
ELSEIF ARG:1 < 17
	CALL ATTACK_SINGLE,ARG,POS(ARG:1),3545
ELSE
	CALL ATTACK_FIELD,ARG,ARG:1,3545
ENDIF
WAIT
;CALL SKILLGAGE_H_SET,ARG,3545,0



;====================================================
;실행내용 여기까지
;====================================================

;====================================================
;追加コマンド(一番下)
;====================================================
@SKILL_SPECIAL_COMMAND_3545,ARG,LOCAL
#LOCALSIZE 1
IF SKILLGAGE_D_GET(ARG,3545) == 0 && SKILLGAGE_F_GET(ARG,3545) > 49
	PRINTFORML [{LOCAL}] 『리본ㆍ부스트』
	C:LOCAL = 13545
	LOCAL += 1
ENDIF
RETURN LOCAL
;====================================================
;コマンド実行
;====================================================
@INPUT_COM_13545,ARG
#LOCALSIZE 1
CALL TRUE_SKILL_COST,ARG,3545
PRINTL [1] 부스트ㆍ서니 빔　　　　　（화염추격　속도ㆍ공격 강화）
PRINTL [2] 부스트ㆍ블루 스타　　　　（비구추격　속도ㆍ방어 강화）
PRINTL [3] 부스트ㆍ힐링 스태프　　　（충격추격　속도 2단계 강화）
PRINTL [4] 부스트ㆍ캐롯 슈터　　　　（핵열추격　속도ㆍ마법위력 강화）
DRAWLINE
PRINTL [0] ＣＡＮＣＥＬ
CALL INPUTINT(0,1,2,3,4)
IF RESULT > 0
	CALL SKILLGAGE_H_SET,ARG,3545,RESULT
	SETCOLOR 0x33ffcc
	PRINTFORML ┏>CHARA━━━━━━━━━━┓　┏>ACT━━━━━━━━━━━━━━━━━━┓
	PRINTFORM ┃[{CPOS(ARG),2}] %CALLNAME:ARG,21,LEFT%┃　┃
	CALLFORM SKILL_NAME_3545,ARG
	PRINTFORML %RESULTS,40,LEFT%┃
	PRINTFORMW ┗━━━━━━━━━━━━━┛　┗━━━━━━━━━━━━━━━━━━━━┛
	RESETCOLOR
	SWAP TARGET,ARG
	CALL ACTION_3545,TARGET
	CALL 구상호출 , "BATTLE_MESSAGE" , TARGET , TARGET
	SWAP TARGET,ARG
ENDIF
RETURN -1

;　 설명문 표시
@SKILL_EXPLAIN_3545
PRINTL 패시브：리본의 하이퍼 시스템 제어용 스킬。
PRINTL 
PRINTL 마법으로 에리나를 서포트할 때 필요한、리본의 부스트 포인트를 가시화한 것。
PRINTL 게이지 50％ 이상에서 커맨드가 사용 가능하게 된다。발동 후、선택에 따른 버프를 아군 전체에 부여하고、
PRINTL 일정 턴 에리나가 공격한 대상에 대해、『만능 속성과 대응 속성의 복합』으로 소위력의 물리 추격을 행한다。
PRINTL 효과 시간은 3턴。게이지 100％에서 발동했을 경우 6턴。발동시 종료될 때까지 해제 불가。
PRINTL 게이지는 평상시 행동으로 데미지양에 따른 양、기합ㆍ포효ㆍ도발ㆍ컨센트레이트로 25% 상승한다。
PRINTL 
PRINTL 에리나와 함께 모험을 하면서 모은 지팡이를 이용한、리본의 기술。（에리나 전용）

;계승에 필요한 부위
@계승부위_3545,ARG
RETURN 1
;とくにない

;사용조건
@SKILL_SPECIAL_ACTIONABLE_3545,ARG
RETURN 0

@SKILLGAGE_DISPLAY_3545,ARG
SETCOLOR 0x999999
IF SKILLGAGE_D_GET(ARG,3545) > 0
	;サニービーム　火炎
	SIF SKILLGAGE_H_GET(ARG,3545) == 1
		PRINTFORML ※부스트ㆍ서니 빔！　나머지 {SKILLGAGE_D_GET(ARG,3545)}턴
	;ブルースター　飛具
	SIF SKILLGAGE_H_GET(ARG,3545) == 2
		PRINTFORML ※부스트ㆍ블루 스타！　나머지 {SKILLGAGE_D_GET(ARG,3545)}턴
	;ヒーリングスタッフ
	SIF SKILLGAGE_H_GET(ARG,3545) == 3
		PRINTFORML ※부스트ㆍ힐링 스태프！　나머지 {SKILLGAGE_D_GET(ARG,3545)}턴
	;キャロットシューター
	SIF SKILLGAGE_H_GET(ARG,3545) == 4
		PRINTFORML ※부스트ㆍ캐롯 슈터！　나머지 {SKILLGAGE_D_GET(ARG,3545)}턴
ELSEIF SKILLGAGE_F_GET(ARG,3545) == 100
		PRINTFORML ※리본ㆍ부스트　마력축적량　MAX！！
ELSEIF SKILLGAGE_F_GET(ARG,3545) < 50
		PRINTFORML ※리본ㆍ부스트　마력축적량　{SKILLGAGE_F_GET(ARG,3545),3}％
ELSEIF SKILLGAGE_F_GET(ARG,3545) < 100
		PRINTFORML ※리본ㆍ부스트　마력축적량　{SKILLGAGE_F_GET(ARG,3545),3}％ Boost OK！
ENDIF
RESETCOLOR

@SKILLGAGE_CHARGE_3545,ARG,ARG:1
#LOCALSIZE 1
;スキルゲージDがある＝発動中の場合離脱
SIF SKILLGAGE_D_GET(ARG,3545) > 0
	RETURN 0
	
;自分の行動の場合
IF ARG == ARG:1
	
	;ダメージを受けた敵の数に応じてスキルゲージアップ
	FOR LOCAL,0,CHARANUM
		SIF CFLAG:LOCAL:PT플래그 == CFLAG:ARG:PT플래그
			CONTINUE
		IF CFLAG:LOCAL:현데미지 > 0
			CALL SKILLGAGE_F_ADD,ARG,3545,5
		ELSE
			CONTINUE
		ENDIF
		;会心、弱点を出すとさらにアップ
		SIF GETBIT(CFLAG:LOCAL:받음플래그,21)
			CALL SKILLGAGE_F_ADD,ARG,3545,5
		SIF GETBIT(CFLAG:LOCAL:받음플래그,22)
			CALL SKILLGAGE_F_ADD,ARG,3545,5
	NEXT
	;気合を入れても上がる
	SIF CFLAG:ARG:입력행동 == 511
		CALL SKILLGAGE_F_ADD,ARG,3545,15
	;集中しても上がる
	SIF CFLAG:ARG:입력행동 == 512
		CALL SKILLGAGE_F_ADD,ARG,3545,15
	;雄叫びしても上がる
	SIF CFLAG:ARG:입력행동 == 514
		CALL SKILLGAGE_F_ADD,ARG,3545,15
	;挑発しても上がる
	SIF CFLAG:ARG:입력행동 == 515
		CALL SKILLGAGE_F_ADD,ARG,3545,15
ENDIF
;ダメージを受けた又はカウンター、トリガー以外の攻撃を受けたら
;ゲージが増える
IF CFLAG:ARG:현데미지 > 0 || GETBIT(CFLAG:ARG:받음플래그,18) || GETBIT(CFLAG:ARG:받음플래그,19)
	LOCAL = 60 * CFLAG:ARG:현데미지 / MAXBASE:ARG:ＨＰ
	LOCAL = LOCAL < 2 ? 2 # LOCAL
	CALL SKILLGAGE_F_ADD,ARG,3545,LOCAL
ENDIF
;スキルゲージが100以上だと100にする
SIF SKILLGAGE_F_GET(ARG,3545) > 100
	CALL SKILLGAGE_F_SET,ARG,3545,100
RETURN 0

@계승불능_3545
RETURN 1

