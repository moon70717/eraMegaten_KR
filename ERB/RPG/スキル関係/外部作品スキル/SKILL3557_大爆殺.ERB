;通常攻撃
;スキルのテンプレートになっています

;スキル名
@SKILL_NAME_3557,ARG
;修羅
;SIF SKILLGAGE_F_GET(ARG,[[스킬:분노폭발]]) == 0
	;こちらがCCOが元ネタの技となっております
	RESULTS = 대폭살
	SIF SKILLGAGE_H_GET(ARG,3557) == 1
		RESULTS += "ㆍ재염1"
	SIF SKILLGAGE_H_GET(ARG,3557) == 2
		RESULTS += "ㆍ재염2"
	SIF SKILLGAGE_H_GET(ARG,3557) == 3
		RESULTS += "ㆍ재염3"
;羅刹
SIF ARG > -1 && SKILLGAGE_F_GET(ARG,[[스킬:분노폭발]]) == 1
	RESULTS = 맹염각

;　명중
@SKILL_HITRATE_3557, ARG
RETURN 100

;　위력
@SKILL_POWER_3557, ARG
IF ARG > -1
	;修羅
	IF SKILLGAGE_F_GET(ARG,[[스킬:분노폭발]]) == 0
		SIF SKILLGAGE_H_GET(ARG,3557) == 0
			RETURN 100
		SIF SKILLGAGE_H_GET(ARG,3557) == 1
			RETURN 205
		SIF SKILLGAGE_H_GET(ARG,3557) == 2
			RETURN 410
		SIF SKILLGAGE_H_GET(ARG,3557) == 3
			RETURN 820
	ENDIF
	;羅刹
	SIF SKILLGAGE_F_GET(ARG,[[스킬:분노폭발]]) == 1
		RETURN 350
ENDIF
RETURN 100

;　최저공격횟수
@SKILL_MINATTACKNUMBER_3557,ARG
RETURN 1

;　최대공격횟수
@SKILL_MAXATTACKNUMBER_3557,ARG
RETURN 1

;　2 ＨＰ지불　3 ＭＰ지불
@SKILL_COSTTYPE_3557, ARG
RETURN 3
;消費なし＝1

;　코스트량
@SKILL_COST_3557,ARG
IF ARG > -1
	;修羅
	IF SKILLGAGE_F_GET(ARG,[[스킬:분노폭발]]) == 0
		SIF SKILLGAGE_H_GET(ARG,3557) == 0
			RETURN 0
		SIF SKILLGAGE_H_GET(ARG,3557) == 1
			RETURN 3
		SIF SKILLGAGE_H_GET(ARG,3557) == 2
			RETURN 22
		SIF SKILLGAGE_H_GET(ARG,3557) == 3
			RETURN 52
	ENDIF
	;羅刹
	SIF SKILLGAGE_F_GET(ARG,[[스킬:분노폭발]]) == 1
		RETURN 25
ENDIF
RETURN 0

;　상성
@SKILL_TYPE_3557,ARG
SIF ARG < 0 || SKILLGAGE_F_GET(ARG,[[스킬:분노폭발]]) == 0
	RETURN 4
SIF 스킬속성표시중 || CFLAG:ARG:타겟 > 16 || CFLAG:ARG:타겟 < 1
	RETURN 4
LOCAL = POS(CFLAG:ARG:타겟)
SIF LOCAL < 0
	RETURN 4
LOCALS = 화염
LOCALS:1 = 타격
LOCALS:2 = 화염
FOR LOCAL:1, 1, 3
	;判定用に数値を仕舞う。反射は-200扱い
	LOCAL:3 = MAXBASE:LOCAL:(LOCALS:(LOCAL:1)) == 999 ? -200 # MAXBASE:LOCAL:(LOCALS:(LOCAL:1))
	LOCAL:3 = BTL_CHK_SKILL_관통(ARG) && INRANGE(MAXBASE:LOCAL:(LOCALS:(LOCAL:1)), 0, 100) ? 100 # LOCAL:3
	LOCAL:3 = CFLAG:LOCAL:(LOCALS:(LOCAL:1)+"가드킬") && LOCAL:3 < 100 ? 100 # LOCAL:3
	LOCAL:3 = CFLAG:LOCAL:(LOCALS:(LOCAL:1)+"무효화횟수") ? 0 # LOCAL:3
	LOCAL:3 = GET_TYPE_NUM(LOCALS:(LOCAL:1)) < 17 && CFLAG:LOCAL:무효플래그 ? 0 # LOCAL:3
	IF GET_TYPE_NUM(LOCALS:(LOCAL:1)) < 4
		LOCAL:3 = CFLAG:LOCAL:물리반사플래그 ? -200 # LOCAL:3
	ELSEIF GET_TYPE_NUM(LOCALS:(LOCAL:1)) < 17
		LOCAL:3 = CFLAG:LOCAL:마법반사플래그 ? -200 # LOCAL:3
	ENDIF
	IF LOCAL:3 > LOCAL:2
		LOCAL:2 = LOCAL:3
		LOCALS = %LOCALS:(LOCAL:1)%
	ENDIF
NEXT
RETURN GET_TYPE_NUM(LOCALS)

;　1 EXTRA　2 MAGIC
@SKILL_DECIDE_TYPE_3557, ARG
RETURN 1
;どちらでもない

;　1 물리데미지로 계산　2 마법데미지로 계산
@SKILL_DAMAGETYPE_3557, ARG
RETURN 1

;　対象が1敵か2味方か3場全体か4特殊か
@SKILL_TARGET_3557
RETURN 1

;　사거리
@SKILL_RANGE_3557,ARG
RETURN 1

;　범위　1 1체　2 1열　3 전체　4 적아군전체
@SKILL_SPHERE_3557,ARG
RETURN 1

;　 효과타입　1 공격、2 회복、3 상태변화、4 특수、5 상태회복
@SKILL_EFECT_3557
RETURN 1

;　 특수처리 여부 및 처리내용
@SKILL_SPECIALEFECT_3557,ARG
;ARG = 1인 경우는 처리 여부를 반환합니다
SIF ARG == 1
	RETURN 0
;아래쪽에 특수처리를 기술합니다

;계승타입
@SKILL_SUCCESSION_TYPE_3557
RETURN 7
;검격

;속도보정
@SKILL_SPEED_3557, ARG = -1
RETURN 0

;　口上の表示とそのタイプ
@SKILL_MESSAGE_3557,ARG


;전투시 이외에 사용 가능한가
@SKILL_ACTIONABLE_FIELD_3557
RETURN 0

;스킬변화？용 랭크
@SKILL_RANK_3557
RETURN 23

@SKILL_RANKUP_3557
RETURN 0

;템플릿용、추가효과 시리즈
;추가효과 스테이터스
@SKILL_ADDTIONAL_STATE_3557,ARG
SIF ARG < 0 || (SKILLGAGE_F_GET(ARG,[[스킬:분노폭발]]) == 0 && SKILLGAGE_H_GET(ARG,3557) == 0)
	RETURN 0
RETURN 18

;추가효과 상성
@SKILL_ADDTIONAL_TYPE_3557,ARG
SIF ARG < 0 || (SKILLGAGE_F_GET(ARG,[[스킬:분노폭발]]) == 0 && SKILLGAGE_H_GET(ARG,3557) == 0)
	RETURN 0
RETURN 4

;추가효과 기본부여확률
@SKILL_ADDTIONAL_BASE_RATE_3557,ARG
IF ARG > -1
	;修羅
	IF SKILLGAGE_F_GET(ARG,[[스킬:분노폭발]]) == 0
		SIF SKILLGAGE_H_GET(ARG,3557) == 0
			RETURN 0
		SIF SKILLGAGE_H_GET(ARG,3557) == 1
			RETURN 10
		SIF SKILLGAGE_H_GET(ARG,3557) == 2
			RETURN 20
		SIF SKILLGAGE_H_GET(ARG,3557) == 3
			RETURN 30
	ENDIF
	;羅刹
	SIF SKILLGAGE_F_GET(ARG,[[스킬:분노폭발]]) == 1
		RETURN 60
ENDIF
RETURN 0

;추가효과 부여확률상한
@SKILL_ADDTIONAL_MAX_RATE_3557,ARG
RETURN 95

;拡散攻撃用追加処理
@SKILL_SPREAD_3557,ARG
;修羅
IF SKILLGAGE_F_GET(ARG,[[스킬:분노폭발]]) == 0
;羅刹
ELSE
	RETURN 1
ENDIF

;====================================================
;行動によるスキルゲージ操作
;====================================================
@SKILLGAGE_CHARGE_3557,ARG,ARG:1
SIF FLAG:행동순2 < 0 && SKILLGAGE_F_GET(ARG,[[스킬:분노폭발]]) == 0
	CALL SKILLGAGE_H_SET(ARG,3557,MIN(SKILLGAGE_H_GET(ARG,[[스킬:재염]]),SKILLGAGE_F_GET(ARG,3557)))

;====================================================
;스킬 및 행동의 실행
;====================================================

@ACTION_3557,ARG,ARG:1

;修羅
IF SKILLGAGE_F_GET(ARG,[[스킬:분노폭발]]) == 0
	LOCAL = 0
	IF CFLAG:POS(ARG:1):화염가드킬 < 1 && SKILLGAGE_H_GET(ARG,3557) > 1
		LOCAL = 1
		LOCAL:1 = POS(ARG:1)
		SIF SKILLGAGE_H_GET(ARG,3557) > 2 || (MAXBASE:POS(ARG:1):화염 < 100 && MAXBASE:POS(ARG:1):화염 >= 0)
			CFLAG:POS(ARG:1):화염가드킬 = 1
	ENDIF
	
	CALL ATTACK_SINGLE,ARG,POS(ARG:1),3557
	
	SIF LOCAL == 1
		CFLAG:(LOCAL:1):화염가드킬 = 0
	CALL SKILLGAGE_H_ADD(ARG,[[스킬:재염]],-SKILLGAGE_H_GET(ARG,3557))
;羅刹
ELSE
	CALL ATTACK_SPREAD,ARG,ARG:1,3557
ENDIF


;====================================================
;실행내용 여기까지
;====================================================

;====================================================
;追加コマンド(EXTRAの下)
;====================================================
@SKILL_SPECIAL_COMMAND_EXTRA_3557,ARG,LOCAL
#LOCALSIZE 1
IF HAVE_SKILL(ARG,[[스킬:재염]]) && SKILLGAGE_F_GET(ARG,[[스킬:분노폭발]]) == 0
	SIF SKILLGAGE_F_GET(ARG,3557) == 3
		SETCOLOR 0xea5550
	SIF SKILLGAGE_F_GET(ARG,3557) == 2
		SETCOLOR 0xeb6a50
	SIF SKILLGAGE_F_GET(ARG,3557) == 1
		SETCOLOR 0xeb8350
	SIF SKILLGAGE_F_GET(ARG,3557) == 0
		SETCOLOR 0xebeb50

	PRINTFORML [{LOCAL}] 대폭살 재염 출력　{SKILLGAGE_F_GET(ARG,3557)}
	RESETCOLOR
	C:(LOCAL++) = 13557
ENDIF
RETURN LOCAL

;====================================================
;コマンド実行
;====================================================
@INPUT_COM_13557,ARG
LOCAL = SKILLGAGE_F_GET(ARG,3557)
CALL SKILLGAGE_F_SET(ARG,3557,(LOCAL + 1)%4)
CALL SKILLGAGE_H_SET(ARG,3557,MIN(SKILLGAGE_H_GET(ARG,[[스킬:재염]]),SKILLGAGE_F_GET(ARG,3557)))
RETURN -1

;　 説明文の表示
@SKILL_EXPLAIN_3557
IF TFLAG:전투중 == 0
	PRINTL 【수라】
	PRINTL 적 1체에 화염 상성의 물리데미지、재염 사용으로 강화
	PRINTL 【나찰】
	PRINTL 적 1체와 주위에 화염+타격 상성의 물리데미지+FLAME
ELSEIF SKILLGAGE_F_GET(U,[[스킬:분노폭발]]) == 0 || TFLAG:전투중 == 0
	IF HAVE_SKILL(U,[[스킬:재염]])
		PRINTL 적 1체에 화염 속성의 물리공격을 행한다。
		PRINTL 【재염･0】소위력의 공격을 행한다
		PRINTL 【재염･1】중위력의 공격을 행한다
		PRINTL 【재염･2】특대위력의 공격을 행하고、화염무효ㆍ화염내성을 관통한다
		PRINTL 【재염･3】극대위력의 공격을 행하고、모든 화염내성을 관통한다
	ELSE
		PRINTL 적 1체에 화염 속성의 소위력의 물리공격을 행한다。코스트소비 없음
	ENDIF
ELSEIF SKILLGAGE_F_GET(U,[[스킬:분노폭발]]) == 1
	PRINTL 적 1체와 그 주위에 화염+타격의 복합속성의 대위력의 물리공격을 행한다
	PRINTL 추가효과：FLAME
ENDIF

;계승에 필요한 부위
@계승부위_3557,ARG
RETURN 1
;とくにない

@SKILL_ACTIONABLE_BATTLE_3557
RETURN 1

;사용조건
@SKILL_SPECIAL_ACTIONABLE_3557,ARG
RETURN 1
;とくにない

