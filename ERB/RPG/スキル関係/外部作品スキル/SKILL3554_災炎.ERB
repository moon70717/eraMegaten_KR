;通常攻撃
;スキルのテンプレートになっています

;スキル名
@SKILL_NAME_3554,ARG
;修羅
;SIF SKILLGAGE_F_GET(ARG,[[스킬:분노폭발]]) == 0
	RESULTS = 재염
;羅刹
SIF ARG > -1 && SKILLGAGE_F_GET(ARG,[[스킬:분노폭발]]) == 1
	RESULTS = 염열옥

;　명중
@SKILL_HITRATE_3554, ARG
RETURN 100

;　위력
@SKILL_POWER_3554, ARG
IF ARG > -1
	;修羅
	SIF SKILLGAGE_F_GET(ARG,[[스킬:분노폭발]]) == 0
		RETURN 0
	;羅刹
	IF SKILLGAGE_F_GET(ARG,[[스킬:분노폭발]]) == 1
		SIF FLAG:확산플래그 == 1
			RETURN 150 + (SKILLGAGE_D_GET(ARG,3554) > 0) * 100
		RETURN 100 + (SKILLGAGE_D_GET(ARG,3554) > 0) * 100
	ENDIF
ENDIF
RETURN 0

;　최저공격횟수
@SKILL_MINATTACKNUMBER_3554,ARG
RETURN 1

;　최대공격횟수
@SKILL_MAXATTACKNUMBER_3554,ARG
RETURN 1

;　2 ＨＰ지불　3 ＭＰ지불
@SKILL_COSTTYPE_3554, ARG
RETURN 3
;消費なし＝1

;　코스트량
@SKILL_COST_3554,ARG
RETURN 15

;　상성
@SKILL_TYPE_3554,ARG
RETURN 4

;　1 EXTRA　2 MAGIC
@SKILL_DECIDE_TYPE_3554, ARG
RETURN 1
;どちらでもない

;　1 물리데미지로 계산　2 마법데미지로 계산
@SKILL_DAMAGETYPE_3554, ARG
RETURN 0


;　対象が1敵か2味方か3場全体か4特殊か
@SKILL_TARGET_3554
LOCAL = SKILL_USE_CHARA()
IF CHARANUM_CHECK(LOCAL)
	;修羅
	SIF SKILLGAGE_F_GET(LOCAL,[[스킬:분노폭발]]) == 0
		RETURN 4
	;羅刹
	SIF SKILLGAGE_F_GET(LOCAL,[[스킬:분노폭발]]) == 1
		RETURN 1
ENDIF
RETURN 4

;　사거리
@SKILL_RANGE_3554,ARG
RETURN 1

;　범위　1 1체　2 1열　3 전체　4 적아군전체
@SKILL_SPHERE_3554,ARG
IF ARG > -1
	;修羅
	SIF SKILLGAGE_F_GET(ARG,[[스킬:분노폭발]]) == 0
		RETURN 4
	;羅刹
	SIF SKILLGAGE_F_GET(ARG,[[스킬:분노폭발]]) == 1
		RETURN 3
ENDIF
RETURN 3

;　 효과타입　1 공격、2 회복、3 상태변화、4 특수、5 상태회복
@SKILL_EFECT_3554
LOCAL = SKILL_USE_CHARA()
IF CHARANUM_CHECK(LOCAL)
	;修羅
	SIF SKILLGAGE_F_GET(LOCAL,[[스킬:분노폭발]]) == 0
		RETURN 4
	;羅刹
	SIF SKILLGAGE_F_GET(LOCAL,[[스킬:분노폭발]]) == 1
		RETURN 3
ENDIF
RETURN 4


;　 특수처리 여부 및 처리내용
@SKILL_SPECIALEFECT_3554,ARG
;ARG = 1인 경우는 처리 여부를 반환합니다
SIF ARG == 1
	RETURN 0
;아래쪽에 특수처리를 기술합니다

;계승타입
@SKILL_SUCCESSION_TYPE_3554
RETURN 22
;검격

;속도보정
@SKILL_SPEED_3554, ARG = -1
RETURN 0

;　口上の表示とそのタイプ
@SKILL_MESSAGE_3554,ARG


;전투시 이외에 사용 가능한가
@SKILL_ACTIONABLE_FIELD_3554
RETURN 0

;스킬변화？용 랭크
@SKILL_RANK_3554
RETURN 23

@SKILL_RANKUP_3554
RETURN 0

;템플릿용、추가효과 시리즈
;추가효과 스테이터스
@SKILL_ADDTIONAL_STATE_3554,ARG
IF ARG > -1
	;羅刹
	SIF SKILLGAGE_F_GET(ARG,[[스킬:분노폭발]]) == 1 && !FLAG:카운터중
		RETURN 18
ENDIF
RETURN 0

;추가효과 상성
@SKILL_ADDTIONAL_TYPE_3554,ARG
IF ARG > -1
	;羅刹
	SIF SKILLGAGE_F_GET(ARG,[[스킬:분노폭발]]) == 1 && !FLAG:카운터중
		RETURN 4
ENDIF
RETURN 0

;추가효과 기본부여확률
@SKILL_ADDTIONAL_BASE_RATE_3554,ARG
IF ARG > -1
	;羅刹
	SIF SKILLGAGE_F_GET(ARG,[[스킬:분노폭발]]) == 1 && !FLAG:카운터중
		RETURN 70
ENDIF
RETURN 0

;추가효과 부여확률상한
@SKILL_ADDTIONAL_MAX_RATE_3554,ARG
IF ARG > -1
	;羅刹
	SIF SKILLGAGE_F_GET(ARG,[[스킬:분노폭발]]) == 1 && !FLAG:카운터중
		RETURN 100
ENDIF
RETURN 0

;====================================================
;トリガー発動条件
;====================================================
@SKILL_TRIGGER_ACTIONABLE_3554,ARG,ARG:1
;修羅
IF SKILLGAGE_F_GET(ARG,[[스킬:분노폭발]]) == 0
	;自分じゃなければNG
	SIF ARG != ARG:1
		RETURN 0
	;3まで
	SIF SKILLGAGE_H_GET(ARG,3554) >= 3
		RETURN 0
	LOCAL:1 = 0
	;クリティカル又は弱点を受けているキャラを探す
	;死んでる可能性があるのでキャラ全体くまなく探す
	FOR LOCAL,0,CHARANUM
		SIF CFLAG:LOCAL:PT플래그 == CFLAG:ARG:PT플래그
			CONTINUE
		SIF RECEIVE_CHECK(LOCAL,"회심") && LOCAL:1 != 41
			LOCAL:1 += 41
		SIF RECEIVE_CHECK(LOCAL,"약점") && LOCAL:1 != 40
			LOCAL:1 += 40
		SIF 100 < LOCAL:1
			BREAK
	NEXT
	LOCAL:1 /= 20
	;40%か80%
	SIF RAND:5 < LOCAL:1
		RETURN 1
	RETURN 0
;羅刹
ELSE
	;自分の場合且つ、怒り奥義使用後
	SIF ARG == ARG:1 && SKILLGAGE_D_GET(ARG,3554) > 0
		GOTO GOKUEN
	IF CFLAG:(ARG):PT플래그 == 0
		LOCAL:1 = 1
		LOCAL:2 = 7
	ELSE
		LOCAL:1 = 7
		LOCAL:2 = 17
	ENDIF
	;炎上状態の敵が居るか見る
	FOR LOCAL,LOCAL:1,LOCAL:2
		SIF SKILLGAGE_D_GET(ARG,3554) > 0
			BREAK
		IF POS(LOCAL) > -1
			SIF CFLAG:POS(LOCAL):상태이상 == GET_STATE_NUM("FLAME")
				BREAK
		ENDIF
		SIF LOCAL == LOCAL:2 - 1
			RETURN 0
	NEXT
	;火炎又は炎上を受けているキャラを探す
	;死んでる可能性があるのでキャラ全体くまなく探す
	FOR LOCAL,0,CHARANUM
		IF CFLAG:LOCAL:PT플래그 != CFLAG:ARG:PT플래그
			SIF RECEIVE_CHECK(LOCAL,"화염") || RECEIVE_CHECK(LOCAL,"FLAME")
				BREAK
		ENDIF
		SIF LOCAL == CHARANUM - 1
			RETURN 0
	NEXT
	$GOKUEN
	RESULT:1 = 22
	SIF CFLAG:(ARG):PT플래그 == 0
		RESULT:1 = 19
	RETURN 1
ENDIF
RETURN 0

;====================================================
;스킬 및 행동의 실행
;====================================================

@ACTION_3554,ARG,ARG:1

;修羅
IF SKILLGAGE_F_GET(ARG,[[스킬:분노폭발]]) == 0
	IF SKILLGAGE_H_GET(ARG,3554) >= 3
		PRINTL 失敗した！
		RETURN 0
	ENDIF
	PRINTFORM %CALLNAME:ARG%の
	CALL SKILLGAGE_H_ADD,ARG,3554,1
	IF !FLAG:카운터중
		CFLAG:ARG:공격강화 = MIN(32, CFLAG:ARG:공격강화 + 4)
		CFLAG:ARG:마법위력강화 = MIN(32, CFLAG:ARG:마법위력강화 + 4)
		PRINT 공격ㆍ마법위력이 높아지고、
	ENDIF
	PRINTL 재염이 하나 늘었다。
	SIF SKILLGAGE_H_GET(ARG,3554) >= 3
		PRINTL 재염이 최대가 되었다。
	WAIT
;羅刹
ELSE
	IF !FLAG:카운터중
		SELECTCASE ARG:1
		CASE 1 TO 6,17,18
			ARG:1 = 19
		CASE 7 TO 16,20,21
			ARG:1 = 22
		CASE 23
			IF CFLAG:(ARG):PT플래그 == 0
				ARG:1 = 19
			ELSE
				ARG:1 = 22
			ENDIF
		ENDSELECT
		
		CALL RATE_ATTACK_FIELD,ARG,ARG:1,3554,-1,200 * (1 + (SKILLGAGE_D_GET(ARG,3554) > 0))
	ELSE
		IF ARG:1 == 19
			LOCAL:1 = 1
			LOCAL:2 = 7
		ELSE
			LOCAL:1 = 7
			LOCAL:2 = 17
		ENDIF
		;炎上状態の敵が居るか見る
		FOR LOCAL,LOCAL:1,LOCAL:2
			SIF POS(LOCAL) < 0
				CONTINUE
			SIF CFLAG:POS(LOCAL):상태이상 == GET_STATE_NUM("DYING")
				CONTINUE
			IF CFLAG:POS(LOCAL):상태이상 == GET_STATE_NUM("FLAME")
				FLAG:확산플래그 = 1
				CALL RATE_ATTACK_SINGLE,ARG,POS(LOCAL),3554,1,300 * (1 + (SKILLGAGE_D_GET(ARG,3554) > 0))
			ELSE
				CALL RATE_ATTACK_SINGLE,ARG,POS(LOCAL),3554,-1,200 * (1 + (SKILLGAGE_D_GET(ARG,3554) > 0))
			ENDIF
			FLAG:확산플래그 = 0
		NEXT
	ENDIF
ENDIF


;====================================================
;실행내용 여기까지
;====================================================

;====================================================
;戦闘中スキルゲージの可視化
;====================================================
@SKILLGAGE_DISPLAY_3554,ARG
;修羅
IF SKILLGAGE_F_GET(LOCAL,[[스킬:분노폭발]]) == 0 && SKILLGAGE_H_GET(ARG,3554) > 0
	SIF SKILLGAGE_H_GET(ARG,3554) == 3
		SETCOLOR 0xea5550
	SIF SKILLGAGE_H_GET(ARG,3554) == 2
		SETCOLOR 0xeb6a50
	SIF SKILLGAGE_H_GET(ARG,3554) == 1
		SETCOLOR 0xeb8350
	SIF SKILLGAGE_H_GET(ARG,3554) == 0
		SETCOLOR 0xebeb50
	PRINTFORML 재염　%"●" * SKILLGAGE_H_GET(ARG,3554)%
;羅刹
ELSEIF SKILLGAGE_F_GET(LOCAL,[[스킬:분노폭발]]) == 1 && SKILLGAGE_D_GET(ARG,3554) > 0
	SETCOLOR COLOR("赤")
	PRINTFORML 염열옥 강화 중　나머지 {SKILLGAGE_D_GET(ARG,3554)}턴
ENDIF
RESETCOLOR

;　 説明文の表示
@SKILL_EXPLAIN_3554
IF TFLAG:전투중 == 0
	PRINTL 【수라】
	PRINTL 사용 시 자신의 공격、마법위력이 상승하고 재염이 하나 쌓인다。
	PRINTL 자신의 공격으로 회심、약점을 맞히면 재염이 하나 쌓인다。
	PRINTL 【나찰】
	PRINTL 적 전체에 화염 비율데미지+FLAME
	PRINTL 자신 또는 아군이 화염 또는 FLAME 효과의 공격을 행해、FLAME 상태의 적이 있으면 발동
	PRINTL 적 전체에 화염 비율데미지
ELSEIF SKILLGAGE_F_GET(U,[[스킬:분노폭발]]) == 0 || TFLAG:전투중 == 0
	PRINTL 자신의 공격、마법위력이 상승하고 재염이 하나 쌓인다。재염은 3개까지 쌓을 수 있다
	PRINTL 재염이 최대면 실패한다
	PRINTL 트리거：자신의 공격으로 회심、약점을 맞히면 재염이 하나 쌓인다。
ELSEIF SKILLGAGE_F_GET(U,[[스킬:분노폭발]]) == 1
	PRINTL 하늘을 우러러 포효하여、마그마의 호수 같은 것은 만들어낸다
	PRINTL 적 전체에 화염 속성으로 대상의 현재 HP의 10％데미지(상한 200)를 주고、FLAME을 부여한다
	PRINTL 트리거：자신 또는 아군의 화염 공격이나 FLAME 공격과、FLAME 상태의 적이 있으면 적 전체에 비율데미지
	PRINTL FLAME 상태의 적에게 화염 속성으로 대상의 최대 HP의 15％데미지(상한 300)를 준다。
	PRINTL 그 이외의 적에게 화염 속성으로 대상의 현재 HP의 10％데미지(상한 200)를 준다。
ENDIF

;계승에 필요한 부위
@계승부위_3554,ARG
RETURN 1
;とくにない

@SKILL_ACTIONABLE_BATTLE_3554
RETURN 1

;사용조건
@SKILL_SPECIAL_ACTIONABLE_3554,ARG
RETURN 1
;とくにない

