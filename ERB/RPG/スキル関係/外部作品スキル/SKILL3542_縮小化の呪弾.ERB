;스킬명
@SKILL_NAME_3542,ARG
;ハイパーモード時
IF ARG >= -1 && SKILLGAGE_D_GET(ARG,3543)
	RESULTS = 지난날의 침열탄
ELSE
	RESULTS = 축소화의 주탄
ENDIF

;　명중
@SKILL_HITRATE_3542, ARG
;ハイパーモード時
SIF ARG == -1
	RETURN 100
IF SKILLGAGE_D_GET(ARG,3543)
	RETURN 150
ELSE
	RETURN 100
ENDIF

;魔法故必殺なし
@SKILL_CRITICAL_RATE_3542, ARG
RETURN 0

;　위력
@SKILL_POWER_3542, ARG
SIF ARG == -1
	RETURN 200
;ハイパーモード時
IF SKILLGAGE_D_GET(ARG,3543)
	RETURN 400
ELSE
	RETURN 200
ENDIF


;　최저공격횟수
@SKILL_MINATTACKNUMBER_3542,ARG
RETURN 1

;　최대공격횟수
@SKILL_MAXATTACKNUMBER_3542,ARG
RETURN 1

;　2 ＨＰ지불　3 ＭＰ지불
@SKILL_COSTTYPE_3542, ARG
RETURN 3
;소비없음＝1

;　코스트량
@SKILL_COST_3542,ARG
SIF ARG == -1
	RETURN 25
;ハイパーモード時
IF SKILLGAGE_D_GET(ARG,3543)
	RETURN 40
ELSE
	RETURN 25
ENDIF

;　상성
@SKILL_TYPE_3542,ARG
#LOCALSSIZE 5
#LOCALSIZE 4
;ハイパーモード時、複合属性
;飛具ㆍ電撃ㆍ万能のうち、最も効果のある相性で攻撃
IF SKILLGAGE_D_GET(ARG,3543)
	SIF ARG < 0 || 스킬속성표시중
		RETURN GET_TYPE_NUM("비구")
	SIF CFLAG:ARG:타겟 > 16 || CFLAG:ARG:타겟 < 1
		RETURN GET_TYPE_NUM("비구")
	LOCAL = POS(CFLAG:ARG:타겟)
	SIF LOCAL < 0
		RETURN GET_TYPE_NUM("비구")
		
	LOCAL:2 = -300
	LOCALS = 비구
	LOCALS:1 = 비구
	LOCALS:2 = 만능
	LOCALS:3 = 전격
	FOR LOCAL:1, 1, 4
		;判定用に数値を仕舞う。反射は-200扱い
		LOCAL:3 = MAXBASE:LOCAL:(LOCALS:(LOCAL:1)) == 999 ? -200 # MAXBASE:LOCAL:(LOCALS:(LOCAL:1))
		LOCAL:3 = !(HAVE_SKILL(ARG,[[스킬:영세라이도우]]) || HAVE_SKILL(ARG,2947)) && CFLAG:LOCAL:(LOCALS:(LOCAL:1)+"무효화횟수") ? -5 # LOCAL:3
		LOCAL:3 = CFLAG:LOCAL:(LOCALS:(LOCAL:1)+"가드킬") && LOCAL:3 < 100 ? 100 # LOCAL:3
		SELECTCASE LOCAL:1
			CASE 1
				LOCAL:3 = CFLAG:LOCAL:물리반사플래그 ? -200 # LOCAL:3
			CASE 1 TO 3
				LOCAL:3 = CFLAG:LOCAL:마법반사플래그 ? -200 # LOCAL:3
			CASEELSE
		ENDSELECT
		IF LOCAL:3 > LOCAL:2
			LOCAL:2 = LOCAL:3
			LOCALS = %LOCALS:(LOCAL:1)%
		ENDIF
	NEXT
	PRINT 속성상성：
	PRINTS LOCALS
	RETURN GET_TYPE_NUM(LOCALS)
	
;通常時は비구
ELSE
	RETURN GET_TYPE_NUM("비구")
ENDIF

;　1 EXTRA　2 MAGIC
@SKILL_DECIDE_TYPE_3542, ARG
RETURN 1
;EX

;　1 물리데미지로 계산　2 마법데미지로 계산
@SKILL_DAMAGETYPE_3542, ARG
RETURN 2


;　대상　1 적　2 아군　3 양쪽전체　4 특수
@SKILL_TARGET_3542
RETURN 1

;　사거리
@SKILL_RANGE_3542,ARG
RETURN 3

;　範囲　11체　2列　3전체　4　적아군전체
@SKILL_SPHERE_3542,ARG
RETURN 3

;　 효과타입　1 공격、2 회복、3 상태변화、4 특수、5 상태회복
@SKILL_EFECT_3542
RETURN 1

;　 특수처리 여부 및 처리내용
@SKILL_SPECIALEFECT_3542,ARG
;ARG = 1인 경우는 처리 여부를 반환합니다
SIF ARG == 1
	RETURN 0
;아래쪽에 특수처리를 기술합니다

;계승타입
@SKILL_SUCCESSION_TYPE_3542
RETURN 0
;검격

;속도보정
@SKILL_SPEED_3542, ARG = -1
RETURN 10

;　구상의 표시와 그 타입
@SKILL_MESSAGE_3542,ARG


;전투시 이외에 사용 가능한가
@SKILL_ACTIONABLE_FIELD_3542
RETURN 0

;스킬변화？용 랭크
@SKILL_RANK_3542
RETURN 15


;템플릿용、추가효과 시리즈
;추가효과 스테이터스
@SKILL_ADDTIONAL_STATE_3542,ARG
RETURN 0

;추가효과 상성
@SKILL_ADDTIONAL_TYPE_3542,ARG
RETURN 0

;추가효과 기본부여확률
@SKILL_ADDTIONAL_BASE_RATE_3542,ARG
RETURN 0

;추가효과 부여확률상한
@SKILL_ADDTIONAL_MAX_RATE_3542,ARG
RETURN 0

;====================================================
;스킬 및 행동의 실행
;====================================================
@ACTION_3542,ARG,ARG:1
#LOCALSIZE 3
#DIM 前HP
;ハイパーモード展開時
IF SKILLGAGE_D_GET(ARG,3543)
	CALL ATTACK_FIELD,ARG,ARG:1,3542
ELSE
IF ARG:1 == 17
	LOCAL:1 = 1
	LOCAL:2 = 4
ELSEIF ARG:1 == 18
	LOCAL:1 = 4
	LOCAL:2 = 7
ELSEIF ARG:1 == 19
	LOCAL:1 = 1
	LOCAL:2 = 7
ELSEIF ARG:1 == 20
	LOCAL:1 = 7
	LOCAL:2 = 12
ELSEIF ARG:1 == 21
	LOCAL:1 = 12
	LOCAL:2 = 17
ELSEIF ARG:1 == 22
	LOCAL:1 = 7
	LOCAL:2 = 17
ELSEIF ARG:1 == 23
	LOCAL:1 = 1
	LOCAL:2 = 17
ENDIF
FOR LOCAL,LOCAL:1,LOCAL:2
	SIF POS(LOCAL) < 0 || CFLAG:POS(LOCAL):상태이상 == GET_STATE_NUM("DYING")
		CONTINUE
	前HP = BASE:POS(LOCAL):ＨＰ
	CALL ATTACK_SINGLE,ARG,POS(LOCAL),3542
	IF POS(LOCAL) > -1 && BASE:POS(LOCAL):ＨＰ < 前HP
			CFLAG:POS(LOCAL):회피강화 = MIN(32, CFLAG:POS(LOCAL):회피강화 + 4)
			CFLAG:POS(LOCAL):방어강화 = MAX(-16, CFLAG:POS(LOCAL):방어강화 - 4)
			CFLAG:POS(LOCAL):공격강화 = MAX(-16, CFLAG:POS(LOCAL):공격강화 - 4)
			PRINTFORML 　　TARGET:[{LOCAL,2}] %CALLNAME:POS(LOCAL),20,LEFT%　>>>>>>　회피가 높아지고、공격력ㆍ방어력을 낮췄다！
	ENDIF
NEXT
ENDIF



;====================================================
;실행내용 여기까지
;====================================================

;　 설명문 표시
@SKILL_EXPLAIN_3542
#LOCALSIZE 1
IF CFLAG:(CHARANUM-1):PT플래그 || U < 0 || U >= CHARANUM
	PRINTL 적 전체에 비구 속성으로 중위력의 마법공격을 1회 행한다。
	PRINTL 추가효과：명중한 대상의 회피 상승、공격ㆍ방어 저하。
	PRINTL 극소의 탄환을 발사해、명중한 상대를 축소화시켜 회피를 상승시켜주는 대신、공격、방어를 저해한다。
	PRINTL 
	PRINTL ――이전에는、더욱 강했었다。토끼귀 족　노아의 기술。（전용）
ELSE
	;ハイパーモード時
	IF SKILLGAGE_D_GET(U,3543)
		PRINTL 적 전체에、비구ㆍ전격ㆍ만능의 복합속성으로、특대위력의 마법공격을 1회 행한다。
		PRINTL 무수한 침 형태의 탄을 쏘아、적을 꿰뚫는다。추가효과는 없으나、명중과 파괴력이 뛰어나다。
		PRINTL 
		SETCOLOR 0xDD00DD
		PRINT 지난날의 모습과 함께、힘도 되살아났다。
		RESETCOLOR
		PRINTL 토끼귀 족　노아의 기술。（전용）
	ELSE
		PRINTL 적 전체에 비구 속성으로 중위력의 마법공격을 1회 행한다。
		PRINTL 추가효과：명중한 대상의 회피 상승、공격ㆍ방어 저하。
		PRINTL 극소의 탄환을 발사해、명중한 상대를 축소화시켜 회피를 상승시켜주는 대신、공격、방어를 저해한다。
	ENDIF
ENDIF


;계승에 필요한 부위
@계승부위_3542,ARG
RETURN 1

@계승불능_3542
RETURN 1

;사용조건
@SKILL_SPECIAL_ACTIONABLE_3542,ARG
RETURN 1
;とくにない

