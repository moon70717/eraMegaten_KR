;通常攻撃
;スキルのテンプレートになっています
;スキル名
@SKILL_NAME_3624,ARG
RESULTS = 크리피프렌즈

;　명중
@SKILL_HITRATE_3624, ARG
RETURN 150

;　위력
@SKILL_POWER_3624, ARG
RETURN 0

;　최저공격횟수
@SKILL_MINATTACKNUMBER_3624,ARG
RETURN 1

;　최대공격횟수
@SKILL_MAXATTACKNUMBER_3624,ARG
RETURN 1

;　2 ＨＰ지불　3 ＭＰ지불
@SKILL_COSTTYPE_3624, ARG
RETURN 3
;消費なし＝1

;　코스트량
@SKILL_COST_3624,ARG
LOCAL = SKILLGAGE_H_GET(ARG,3622)
SELECTCASE LOCAL
CASE 0,1,2
	RETURN 10
CASE 3,4,5
	RETURN 14
CASE 6,7,8
	RETURN 18
CASE 9,10
	RETURN 22
ENDSELECT
;　상성
@SKILL_TYPE_3624,ARG
RETURN 11
;주살

;　1 EXTRA　2 MAGIC
@SKILL_DECIDE_TYPE_3624, ARG
RETURN 1
;EXTRA

;　1 물리데미지로 계산　2 마법데미지로 계산
@SKILL_DAMAGETYPE_3624, ARG
RETURN 2

;　対象が1敵か2味方か3場全体か4特殊か
@SKILL_TARGET_3624
RETURN 3

;　사거리
@SKILL_RANGE_3624,ARG
RETURN 3

;　범위　1 1체　2 1열　3 전체　4 적아군전체
@SKILL_SPHERE_3624,ARG
RETURN 4

;　 효과타입　1 공격、2 회복、3 상태변화、4 특수、5 상태회복
@SKILL_EFECT_3624
RETURN 4

;　 특수처리 여부 및 처리내용
@SKILL_SPECIALEFECT_3624,ARG
;ARG = 1인 경우는 처리 여부를 반환합니다
SIF ARG == 1
	RETURN 1
;아래쪽에 특수처리를 기술합니다

;계승타입
@SKILL_SUCCESSION_TYPE_3624
RETURN 5
;지원

;속도보정
@SKILL_SPEED_3624, ARG = -1
RETURN 0

;　口上の表示とそのタイプ
@SKILL_MESSAGE_3624,ARG

;전투시 이외에 사용 가능한가
@SKILL_ACTIONABLE_FIELD_3624
RETURN 0

;스킬변화？용 랭크
@SKILL_RANK_3624
RETURN 16

@계승불능_3624
RETURN 1


;템플릿용、추가효과 시리즈
;추가효과 스테이터스
@SKILL_ADDTIONAL_STATE_3624,ARG
;SPECIAL_ACTIONで呼び出す前にスキルゲージFに対応する数を突っ込んで分岐させる
SELECTCASE  SKILLGAGE_F_GET(ARG,3624)
CASE 1
	;イッショニアソボウ、精神相性PANIC
	RETURN GET_STATE_NUM("PANIC")
CASE 2
	;イドルハンズ、神経相性BIND
	RETURN GET_STATE_NUM("BIND")
CASE 3
	;ポイズンリリックA、PANICかCHARM(精神相性)かPOISONかBIND(神経相性)
	IF RAND:2 == 0
		RETURN GET_STATE_NUM("PANIC")
	ELSE
		RETURN GET_STATE_NUM("CHARM")
	ENDIF
CASE 4
	;ポイズンリリックB、POISONかBIND(神経相性)
	IF RAND:2 == 0
		RETURN GET_STATE_NUM("POISON")
	ELSE
		RETURN GET_STATE_NUM("BIND")
	ENDIF
CASE 5,6
	;コッチニオイデ、呪殺相性DYING(確率小)
	;ニガサナイヨ、呪殺相性DYING(確率中)
	RETURN GET_STATE_NUM("DYING")
CASEELSE
	RETURN 0
ENDSELECT

;추가효과 상성
@SKILL_ADDTIONAL_TYPE_3624,ARG
;SPECIAL_ACTION内でSINGLE_BADSTATEを呼び出す前にスキルゲージFに対応する数を突っ込んで分岐させる。
;呼んだ後はちゃんと0に戻すんだぞ俺。
SELECTCASE  SKILLGAGE_F_GET(ARG,3624)
CASE 1
	;イッショニアソボウ、精神相性PANIC
	RETURN 9
CASE 2
	;イドルハンズ、神経相性BIND
	RETURN 8
CASE 3
	;ポイズンリリックA、PANICかCHARM(精神相性)か
	RETURN 9
CASE 4
	;ポイズンリリックB、POISONかBIND(神経相性)
	RETURN 8
CASE 5,6
	;コッチニオイデ、呪殺相性DYING(確率小)
	;ニガサナイヨ、呪殺相性DYING(確率中)
	RETURN 11
CASEELSE
	RETURN 0
ENDSELECT

;추가효과 기본부여확률
@SKILL_ADDTIONAL_BASE_RATE_3624,ARG
SELECTCASE  SKILLGAGE_F_GET(ARG,3624)
CASE 1
	;イッショニアソボウ、30-65
	RETURN 30
CASE 2
	;イドルハンズ、40-70
	RETURN 40
CASE 3,4
	;ポイズンリリック、60-90
	RETURN 60
CASE 5
	;コッチニオイデ、30-60
	RETURN 30
CASE 6
	;ニガサナイヨ、50-80
	RETURN 50
CASEELSE
	RETURN 0
ENDSELECT

;추가효과 부여확률상한
@SKILL_ADDTIONAL_MAX_RATE_3624,ARG
SELECTCASE  SKILLGAGE_F_GET(ARG,3624)
CASE 1
	;イッショニアソボウ、30-65
	RETURN 65
CASE 2
	;イドルハンズ、40-70
	RETURN 70
CASE 3,4
	;ポイズンリリック、60-90
	RETURN 95
CASE 5
	;コッチニオイデ、30-60
	RETURN 60
CASE 6
	;ニガサナイヨ、50-80
	RETURN 80
CASEELSE
	RETURN 0
ENDSELECT

;====================================================
;カウンター発動条件
;====================================================
@SKILL_COUNTER_ACTIONABLE_3624,ARG,ARG:1
SIF !GETBIT(CFLAG:ARG:받음플래그,18) && !GETBIT(CFLAG:ARG:받음플래그,19)
	RETURN 0
LOCAL:2=0
FOR LOCAL,7,17
	SIF POS(LOCAL)>0 && GET_STATE(CFLAG:(POS(LOCAL)):상태이상) != "DYING"
		LOCAL:2++
NEXT
SIF LOCAL:2 == 0
	RETURN 0
SIF RAND:10 < 5 && SKILLGAGE_D_GET(ARG,3624) > 0
	RETURN 1
RETURN 0
;====================================================
;トリガー発動条件
;====================================================
@SKILL_TRIGGER_ACTIONABLE_3624,ARG,ARG:1
SIF ARG != ARG:1
	RETURN 0
SIF SKILLGAGE_D_GET(ARG,3624) <= 0 || SKILLGAGE_H_GET(ARG,3622) <= 0 
	;活性残ターンが残っていても死霊カウンターが乗ってないなら発動できない
	RETURN 0
IF CFLAG:ARG:입력행동 == 501 || CFLAG:ARG:입력행동 == 507 || CFLAG:ARG:입력행동 == 1423
;タルカジャ、ラクンダ、カースエピタフなら75%で発動
	SIF RAND:100 <= 75
		RETURN 1
ELSEIF CFLAG:ARG:입력행동 == 3622 
;リビングデッド使用時は30%で発動。どんぶり勘定。
	SIF RAND:100 <= 30
		RETURN 1
ENDIF
RETURN 0
;====================================================
;스킬 및 행동의 실행
;====================================================
@ACTION_3624,ARG,ARG:1
IF FLAG:카운터중 
	PRINTL
	;ここからカウンター発動時の処理。
	;スキル名は基本的に原作の何かしらから引用。
	LOCAL = ARG
	;LOCAL:2にクリーピーフレンズの基準レベル(1～4)を入れておく
	LOCAL:2 = SKILLGAGE_H_GET(ARG,3624)
	;追加発動スキルのレベルを選定。
	LOCAL:10 = RAND(1,LOCAL:2 + 1)
	$CREEPYFRIENDS_REACT
	;敵が全員死んでいたらその時点でループ制御に使用しているLOCAL:10を0に
	LOCAL:11 = 0
	FOR LOCAL:5,7,17
		IF POS(LOCAL:5) > 0
			LOCAL:11++
		ENDIF
	NEXT
	SIF LOCAL:11 == 0
		LOCAL:10 = 0

	;追加行動の発動。
	IF RAND:100 <= 75 && LOCAL:10 > 0 
	;PRINTFORML ●追加行動発動:{LOCAL:10}
		;LV4
		IF LOCAL:10 > 3 && RAND:100 <= 75
			IF RAND:4 == 0
				;ランダム敵1～4体にPANICかCHARM(精神相性)かPOISONかBIND(神経相性)。
				;付与率は高いが選定率がまず低いのでバランスが取れて…取れてると…いいな……
				;【シンデレラガールズ】ユニット名
				SETCOLOR 0x33ffcc
				PRINTFORML ┏>CHARA━━━━━━━━━━┓　┏>ACT━━━━━━━━━━━━━━━━━━┓9
				PRINTFORM ┃[{CPOS(LOCAL),2}] %CALLNAME:(LOCAL),21,LEFT%┃　┃
				PRINTFORML %"포이즌 리릭",40,LEFT%┃
				PRINTFORMW ┗━━━━━━━━━━━━━┛　┗━━━━━━━━━━━━━━━━━━━━┛
				RESETCOLOR
				
				LOCAL:4 = RAND(1,4)
				LOCAL:3 = RAND(6,15)
				$CREEPYFRIENDS_09
				LOCAL:3 ++
				SIF LOCAL:3 == 17
					LOCAL:3 = 7
				IF POS(LOCAL:3) > 0 && GET_STATE(CFLAG:POS(LOCAL:3):상태이상) != "DYING"
						PRINTFORM 　　TARGET：[{LOCAL:3,2}]%CALLNAME:POS(LOCAL:3),20,LEFT%　>>>>>　
					IF RAND:2 == 0
						CALL SKILLGAGE_F_SET,ARG,3624,3
					ELSE
						CALL SKILLGAGE_F_SET,ARG,3624,4
					ENDIF
					CALL SINGLE_BADSTATE,ARG,POS(LOCAL:3),3624
					CALL SKILLGAGE_F_SET,ARG,3624,0
				ELSE
					GOTO CREEPYFRIENDS_09
				ENDIF
				IF LOCAL:4 > 0
					LOCAL:4 --
					GOTO CREEPYFRIENDS_09
				ENDIF
				
			ELSE
				;ランダム単体呪殺(確率中)
				;【デッドリーチェイス】特技名
				SETCOLOR 0x33ffcc
				PRINTFORML ┏>CHARA━━━━━━━━━━┓　┏>ACT━━━━━━━━━━━━━━━━━━┓8
				PRINTFORM ┃[{CPOS(LOCAL),2}] ＵＮＫＮＯＷＮ　　　 ┃　┃
				PRINTFORML %"놓치지 않아",40,LEFT%┃
				PRINTFORMW ┗━━━━━━━━━━━━━┛　┗━━━━━━━━━━━━━━━━━━━━┛
				RESETCOLOR
				CALL ACTION_501,POS(LOCAL),22
				LOCAL:3 = RAND(6,15)
				$CREEPYFRIENDS_08
				LOCAL:3 ++
				SIF LOCAL:3 == 17
					LOCAL:3 = 7
				IF POS(LOCAL:3) > 0 && GET_STATE(CFLAG:POS(LOCAL:3):상태이상) != "DYING"
					CALL SKILLGAGE_F_SET,ARG,3624,6
					CALL SINGLE_BADSTATE,ARG,POS(LOCAL:3),3624
					CALL SKILLGAGE_F_SET,ARG,3624,0
				ELSE
					GOTO CREEPYFRIENDS_08
				ENDIF
			ENDIF
		;LV3
		ELSEIF LOCAL:10 > 2 && RAND:100 <= 75
			IF RAND:4 == 0
				;自己限定の能力強化(種類ランダム)
				;これだけオリジナル、それっぽい名前が出てこなかった…
				SETCOLOR 0x33ffcc
				PRINTFORML ┏>CHARA━━━━━━━━━━┓　┏>ACT━━━━━━━━━━━━━━━━━━┓7
				PRINTFORM ┃[{CPOS(LOCAL),2}] ＵＮＫＮＯＷＮ　　　 ┃　┃
				PRINTFORML %"네 옆에 있어",40,LEFT%┃
				PRINTFORMW ┗━━━━━━━━━━━━━┛　┗━━━━━━━━━━━━━━━━━━━━┛
				RESETCOLOR
				SELECTCASE RAND:3
				CASE 0
					CFLAG:(LOCAL):명중강화 = MIN(32, CFLAG:(ARG):명중강화 + 4)
					PRINTFORMW 　　TARGET:[{CPOS(LOCAL),2}] %CALLNAME:ARG,20,LEFT%　>>>>>>　명중이 높아졌다！
				CASE 1
					CFLAG:(LOCAL):마법위력강화 = MIN(32, CFLAG:(ARG):마법위력강화 + 4)
					PRINTFORMW 　　TARGET:[{CPOS(LOCAL),2}] %CALLNAME:ARG,20,LEFT%　>>>>>>　마법위력이 높아졌다！
				CASE 2
					CFLAG:(LOCAL):마법효과강화 = MIN(32, CFLAG:(ARG):마법효과강화 + 4)
					PRINTFORMW 　　TARGET:[{CPOS(LOCAL),2}] %CALLNAME:ARG,20,LEFT%　>>>>>>　마법효과가 높아졌다！
				ENDSELECT
			ELSE
				;스쿤ㆍ다
				;【ダークインバイト】カード名
				SETCOLOR 0x33ffcc
				PRINTFORML ┏>CHARA━━━━━━━━━━┓　┏>ACT━━━━━━━━━━━━━━━━━━┓6
				PRINTFORM ┃[{CPOS(LOCAL),2}] %CALLNAME:(LOCAL),21,LEFT%┃　┃
				PRINTFORML %"다크 인바이트",40,LEFT%┃
				PRINTFORMW ┗━━━━━━━━━━━━━┛　┗━━━━━━━━━━━━━━━━━━━━┛
				RESETCOLOR
				LOCAL:3 = RAND(6,15)
				$CREEPYFRIENDS_06
				LOCAL:3 ++
				SIF LOCAL:3 == 17
					LOCAL:3 = 7
				IF POS(LOCAL:3) > 0 && GET_STATE(CFLAG:POS(LOCAL:3):상태이상) != "DYING"
					CFLAG:(LOCAL):마법위력강화 = MIN(32, CFLAG:(ARG):마법위력강화 - 4)
					PRINTFORMW 　　TARGET:[{(LOCAL:3),2}] %CALLNAME:POS(LOCAL:3),20,LEFT%　>>>>>>　마법위력이 낮아졌다！
					
					CFLAG:(LOCAL):마법효과강화 = MIN(32, CFLAG:(ARG):마법효과강화 - 4)
					PRINTFORMW 　　TARGET:[{(LOCAL:3),2}] %CALLNAME:POS(LOCAL:3),20,LEFT%　>>>>>>　마법효과가 낮아졌다！
				ELSE
					GOTO CREEPYFRIENDS_06
				ENDIF
			ENDIF
		;LV2
		ELSEIF LOCAL:10 > 1 && RAND:100 <= 75
			IF RAND:3 == 0
				;ランダム敵1～3体にBIND(神経相性)
				;【アンデッドㆍダンスロック】コミュイベントエンディング名(Idol Hands)
				SETCOLOR 0x33ffcc
				PRINTFORML ┏>CHARA━━━━━━━━━━┓　┏>ACT━━━━━━━━━━━━━━━━━━┓5
				PRINTFORM ┃[{CPOS(LOCAL),2}] %CALLNAME:(LOCAL),21,LEFT%┃　┃
				PRINTFORML %"아이돌 핸즈",40,LEFT%┃
				PRINTFORMW ┗━━━━━━━━━━━━━┛　┗━━━━━━━━━━━━━━━━━━━━┛
				RESETCOLOR
				
				LOCAL:4 = RAND(1,3)
				LOCAL:3 = RAND(6,15)
				$CREEPYFRIENDS_05
				LOCAL:3 ++
				SIF LOCAL:3 == 17
					LOCAL:3 = 7
				IF POS(LOCAL:3) > 0 && GET_STATE(CFLAG:POS(LOCAL:3):상태이상) != "DYING"
					CALL SKILLGAGE_F_SET,ARG,3624,2
					CALL SINGLE_BADSTATE,ARG,POS(LOCAL:3),3624
					CALL SKILLGAGE_F_SET,ARG,3624,0
				ELSE
					GOTO CREEPYFRIENDS_05
				ENDIF
				IF LOCAL:4 > 0
					LOCAL:4 --
					GOTO CREEPYFRIENDS_05
				ENDIF
			ELSEIF RAND:2==0
				;ランダム単体能力ランダム減少。
				;【小さな恋の密室事件】一節引用
				SETCOLOR 0x33ffcc
				PRINTFORML ┏>CHARA━━━━━━━━━━┓　┏>ACT━━━━━━━━━━━━━━━━━━┓4
				PRINTFORM ┃[{CPOS(LOCAL),2}] ＵＮＫＮＯＷＮ　　　 ┃　┃
				PRINTFORML %"당신은 누구",40,LEFT%┃
				PRINTFORMW ┗━━━━━━━━━━━━━┛　┗━━━━━━━━━━━━━━━━━━━━┛
				RESETCOLOR
				
				LOCAL:3 = RAND(6,15)
				$CREEPYFRIENDS_04
				LOCAL:3 ++
				SIF LOCAL:3 == 17
					LOCAL:3 = 7
				IF POS(LOCAL:3) > 0 && GET_STATE(CFLAG:POS(LOCAL:3):상태이상) != "DYING"
					SELECTCASE RAND:4
					CASE 0
						CFLAG:(POS(LOCAL:3)):마법효과강화 = MIN(32, CFLAG:(POS(LOCAL:3)):마법효과강화 - 4)
					PRINTFORMW 　　TARGET:[{(LOCAL:3),2}] %CALLNAME:POS(LOCAL:3),20,LEFT%　>>>>>>　마법효과가 낮아졌다！
					CASE 1
						CFLAG:(POS(LOCAL:3)):마법위력강화 = MIN(32, CFLAG:(POS(LOCAL:3)):마법위력강화 - 4)
					PRINTFORMW 　　TARGET:[{(LOCAL:3),2}] %CALLNAME:POS(LOCAL:3),20,LEFT%　>>>>>>　마법위력이 낮아졌다！
					CASE 2
						CFLAG:(POS(LOCAL:3)):명중강화 = MIN(32, CFLAG:(POS(LOCAL:3)):명중강화 - 4)
					PRINTFORMW 　　TARGET:[{(LOCAL:3),2}] %CALLNAME:POS(LOCAL:3),20,LEFT%　>>>>>>　명중이 낮아졌다！
					CASE 3
						CFLAG:(POS(LOCAL:3)):회피강화 = MIN(32, CFLAG:(POS(LOCAL:3)):회피강화 - 4)
					PRINTFORMW 　　TARGET:[{(LOCAL:3),2}] %CALLNAME:POS(LOCAL:3),20,LEFT%　>>>>>>　회피가 낮아졌다！
					ENDSELECT
				ELSE
					GOTO CREEPYFRIENDS_04
				ENDIF
				
			ELSE
				;ランダム単体呪殺(確率低)
				;【クリーピーフレンズ】特技名
				SETCOLOR 0x33ffcc
				PRINTFORML ┏>CHARA━━━━━━━━━━┓　┏>ACT━━━━━━━━━━━━━━━━━━┓3
				PRINTFORM ┃[{CPOS(LOCAL),2}] ＵＮＫＮＯＷＮ　　　 ┃　┃
				PRINTFORML %"이쪽으로 와",40,LEFT%┃
				PRINTFORMW ┗━━━━━━━━━━━━━┛　┗━━━━━━━━━━━━━━━━━━━━┛
				RESETCOLOR
				
				LOCAL:3 = RAND(6,15)
				$CREEPYFRIENDS_03
				LOCAL:3 ++
				SIF LOCAL:3 == 17
					LOCAL:3 = 7
				IF POS(LOCAL:3) > 0 && GET_STATE(CFLAG:POS(LOCAL:3):상태이상) != "DYING"
					CALL SKILLGAGE_F_SET,ARG,3624,5
					CALL SINGLE_BADSTATE,ARG,POS(LOCAL:3),3624
					CALL SKILLGAGE_F_SET,ARG,3624,0
				ELSE
					GOTO CREEPYFRIENDS_03
				ENDIF
			ENDIF
		;LV1
		ELSE
			IF RAND:2 == 0
				;ランダム1～2体にバステ付着率上昇
				;【ハスリングコール】特技名
				SETCOLOR 0x33ffcc
				PRINTFORML ┏>CHARA━━━━━━━━━━┓　┏>ACT━━━━━━━━━━━━━━━━━━┓2
				PRINTFORM ┃[{CPOS(LOCAL),2}] ＵＮＫＮＯＷＮ　　　 ┃　┃
				PRINTFORML %"이어져 있어",40,LEFT%┃
				PRINTFORMW ┗━━━━━━━━━━━━━┛　┗━━━━━━━━━━━━━━━━━━━━┛
				RESETCOLOR
				
				IF RAND:10 <= 7
					LOCAL:4 = 1
				ELSE
					LOCAL:4 = 2
				ENDIF
				LOCAL:3 = RAND(6,15)
				$CREEPYFRIENDS_02
				LOCAL:3 ++
				SIF LOCAL:3 == 17
					LOCAL:3 = 7
				IF POS(LOCAL:3) > 0 && GET_STATE(CFLAG:POS(LOCAL:3):상태이상) != "DYING"
					CFLAG:POS(LOCAL:3):BS부착률강화 = MIN(4, CFLAG:POS(LOCAL:3):BS부착률강화 + 4)
					PRINTFORMW 　　TARGET:[{LOCAL:3,2}] %CALLNAME:POS(LOCAL:3),20,LEFT%　>>>>>>　상태이상 부착률이 높아졌다！
				ELSE
					GOTO CREEPYFRIENDS_02
				ENDIF
				IF LOCAL:4 > 0
					LOCAL:4 --
					GOTO CREEPYFRIENDS_02
				ENDIF
			ELSE
				;ランダム1体に精神相性でPANIC
				;【ヨモスガラㆍパーティ】特技名
				SETCOLOR 0x33ffcc
				PRINTFORML ┏>CHARA━━━━━━━━━━┓　┏>ACT━━━━━━━━━━━━━━━━━━┓1
				PRINTFORM ┃[{CPOS(LOCAL),2}] ＵＮＫＮＯＷＮ　　　 ┃　┃
				PRINTFORML %"함께 놀자",40,LEFT%┃
				PRINTFORMW ┗━━━━━━━━━━━━━┛　┗━━━━━━━━━━━━━━━━━━━━┛
				RESETCOLOR
				LOCAL:3 = RAND(6,15)
				$CREEPYFRIENDS_01
				LOCAL:3 ++
				SIF LOCAL:3 == 17
					LOCAL:3 = 7
				IF POS(LOCAL:3) > 0 && GET_STATE(CFLAG:POS(LOCAL:3):상태이상) != "DYING"
					CALL SKILLGAGE_F_SET,ARG,3624,1
					CALL SINGLE_BADSTATE,ARG,POS(LOCAL:3),3624
					CALL SKILLGAGE_F_SET,ARG,3624,0
				ELSE
					GOTO CREEPYFRIENDS_01
				ENDIF
			ENDIF
		ENDIF
	ENDIF
	PRINTL
	SIF RAND:100 <= 80
		LOCAL:10 --
	IF LOCAL:10 > 0
		GOTO CREEPYFRIENDS_REACT
	ENDIF
	RESETCOLOR
ELSE
	;ここから任意発動時の動作。活性化処理。
	;現在の死霊カウンターを取得
	LOCAL = SKILLGAGE_H_GET(ARG,3622)
	SELECTCASE LOCAL
	CASE 0
		PRINTFORM ＞발동할 수 있을 정도로 사령을 소환하지 않았다！
	CASE 1,2
		LOCAL:2 = 1
	CASE 3,4,5
		LOCAL:2 = 2
	CASE 6,7,8
		LOCAL:2 = 3
	CASE 9,10
		LOCAL:2 = 4
	ENDSELECT
	IF LOCAL != 0
		;Ｄゲージ(ターンが減るほど減少)は5に(残ターン管理)
		CALL SKILLGAGE_D_SET(ARG,3624,5)
		;ＨゲージはＣＰ累積量によって変動させる(レベル管理)
		CALL SKILLGAGE_H_SET(ARG,3624,(LOCAL:2))
		PRINTFORML ＞%ARG는% 마력을 해방해、주위의 시령들을 활성화시켰다！
	ENDIF
ENDIF
;@SPECIAL_ACTION_3624,ARG
;====================================================
;실행내용 여기까지
;====================================================

;====================================================
;전투중 이외에서의 실행
;====================================================


;====================================================
;실행내용 여기까지
;====================================================

;====================================================
;戦闘中スキルゲージの可視化
;====================================================
@SKILLGAGE_DISPLAY_3624,ARG
SETCOLOR 0xFF0000
IF SKILLGAGE_D_GET(ARG,3624) > 0
	PRINTFORML ＞크리피프렌즈 LV{SKILLGAGE_H_GET(ARG,3624)} 발동중…　나머지 {SKILLGAGE_D_GET(ARG,3624)}턴
ENDIF
RESETCOLOR

;　 説明文の表示
@SKILL_EXPLAIN_3624
PRINTL 발동자의 주위에 모여든 시령들에게 마력을 건네、활성화시킨다。
PRINTL 5턴간、사령 카운터를 소비하는 스킬을 발동했을 경우、일정 확률로 시령들이 추가행동을 하게 된다。
PRINTL 
PRINTL 이 스킬의 소비MP는 누적된 사령 카운터의 양에 따라 변화한다。
PRINTL 추가행동의 횟수와 종류는、이 스킬을 발동한 시점에서의 사령 카운터에 따라 결정된다。

;계승에 필요한 부위
@계승부위_3624,ARG
RETURN 1

;사용조건
@SKILL_SPECIAL_ACTIONABLE_3624,ARG
SIF SKILLGAGE_H_GET(ARG,3622) >= 1
	RETURN 1
RETURN 0

